on right click with crossbow:

	line 1 of player's tool's lore starts with "&7Ka-Blooey!"
	player's gamemode is not spectator
	
	cancel event

	cooldown of player's tool for player <= 0 seconds
	set the cooldown of player's tool for player to 0.6 seconds
	
	set player's tool to crossbow named "&6Grenade Launcher" with lore "&7Ka-Blooey!" with nbt "{Charged:1b,ChargedProjectiles:[{id:""minecraft:arrow"",Count:1b}],Unbreakable:1,CustomModelData:4,HideFlags:63}"
	getAmmo(player) is not less than 10
	modifyAmmo(player, 10)
	
	set {_loc} to location 1.6 meters above location of player
	play sound "custom.weapon.demo_shoot" at {_loc} for all players
	set {_speed} to 4
	set {_grav} to 0.4
	set {_bounces} to 1
	loop 40 times:
		loop {_speed} times:
			if {_dontshoot} is set:
				stop
				
			loop {deflect::*}:
				distance between {deflect::%loop-index%} and {_loc} is less than 2.5
				set {_loc}'s yaw to {deflect::%loop-index%}'s yaw
				set {_loc}'s pitch to {deflect::%loop-index%}'s pitch
				add 2 to {_speed}
				set {_bounces} to 3
				add 0.6 to {_grav}
				delete {deflect::%loop-index%}
				play sound "custom.weapon.airblast_projectile" at {_loc} for all players
			
			set {_loc} to location 0.5 meters in front of {_loc}
			set {_loc} to location {_grav} meters above {_loc}
			remove 0.1/{_speed} from {_grav}
			
			particle(enchanted_hit, 1, 0, 0, 0, {_loc})
			particle(enchanted_hit, 1, 0.1, 0.1, 0.1, {_loc})
			
			set {_ent::*} to hitbox(player, {_loc})
				
			loop all entities where [{_ent::*} contains input]:
				
				play sound "minecraft:custom.weapon.explode%random integer between 1 and 3%" with volume 2 and pitch (random integer between 0.7 and 1.3) at loop-entity for all players
				particle(explosion_emitter, 1, 0.1, 0.1, 0.1, {_loc})
				set {_dontshoot} to true
				set {_d} to (0.4 / distance between loop-entity and {_loc})
				push loop-entity {_v} with force abs({_d})
				tfdmg(100 * {_bounces}, loop-entity, player, 4)
				
				set {_d} to (0.4 / distance between {_loc} and loop-entity)
				set {_v} to vector from {_loc} to block above
				loop entities in radius 2 around loop-entity:
				
					loop-entity-1 is not loop-entity-2
					set {_dontshoot} to true
					
					set {_d} to (0.4 / distance between loop-entity-1 and loop-entity-2)
					set {_v} to vector from loop-entity-1 to block above loop-entity-2
					push loop-entity-2 {_v} with force abs({_d})
					tfdmg(60 * {_bounces}, loop-entity-2, player, 4)
			
			loop blocks in radius blockRadius(block at {_loc}) around {_loc}:
				isPassableBlock(loop-block) is false
				canPass({_loc}) is false
				set {_loc} to location above {_loc}
				set {_grav} to abs({_grav}/1.1)
				set {_bounces} to {_bounces} * 0.6
				if {_bounces} <= 0.2:
					set {_stop} to true
					exit 2 sections
				exit 1 section
			if {_stop} is true:
				stop loop
					
		wait 1 tick
	
	particle(explosion_emitter, 1, 0.1, 0.1, 0.1, {_loc})
	loop entities in radius 3 around {_loc}:
		canSightline({_loc}, loop-entity) is true
		set {_d} to (0.4 / distance between loop-entity and {_loc})
		push loop-entity {_v} with force abs({_d})
		tfdmg(30, loop-entity, player, 4)
	
	
	