import:
	org.bukkit.event.player.PlayerItemHeldEvent as HotbarSwapEvent
	org.bukkit.entity.Damageable

on HotbarSwapEvent:
	set {_p} to event.getPlayer()
	wait 1 tick
	{_p}'s tool's name is "&6Knife"
	while {_p}'s tool's name is "&6Knife":
		set {_loc} to {_p}'s head location
		set {_loc} to location 1.5 meters in front of {_loc}
		set {_e::*} to all entities in radius 1.5 around {_loc} where [input is not {_p}]
		if size of {_e::*} is less than 1:
			custom model data of {_p}'s tool is not 9
			set custom model data of {_p}'s tool to 9
		loop {_e::*}:
			loop-value's display name does not contain "'s Sentry" or "'s Dispenser"
			set {_v1} to vector from yaw loop-value's yaw and pitch 0
			set {_v2} to vector from yaw {_p}'s yaw and pitch 0
			if ({_v1} dot {_v2}) is more than 0:
				custom model data of {_p}'s tool is not 11
				set custom model data of {_p}'s tool to 11
				set {_stop} to true
			else:
				{_stop} is not true
				custom model data of {_p}'s tool is not 9
				set custom model data of {_p}'s tool to 9
			
		wait 2 ticks

on leftclick:
	player's tool's name is "&6Knife" or "&6Conniver's Kunai"
	player's gamemode is not spectator
	cooldown of player's tool for player <= 0 seconds
	set the cooldown of player's tool for player to 0.8 seconds
	undisguise(player)
	play sound "custom.weapon.melee_swing" with volume 0.5 and pitch 1 at player's head location for all players

on damage:
	damager's tool's name is "&6Knife" or "&6Conniver's Kunai"
	damager is a player
	damager's gamemode is not spectator
	cancel event
	
	set {_model} to 9
	set {_model} to 12 if damager's tool's name is "&6Conniver's Kunai"
	
	distance between location above victim and location above damager is less than 3
	cooldown of damager's tool for damager <= 0 seconds
	# Set cooldown moved near melee hit sound to consolidate for location check
	
	cancel event
	
	set {_loc} to location 1.5 meters above location of damager
	play sound "custom.weapon.melee_swing" with volume 2 and pitch 1 at {_loc} for all players

	loop blocks in radius 0.6 around {_loc}:
		loop-block is solid
		stop
	
	set the cooldown of damager's tool for damager to 0.8 seconds
	
	play sound "custom.weapon.melee_hit" with volume 2 and pitch 1 at {_loc} for all players
	play sound "custom.weapon.melee_hit" with volume 2 and pitch 2 at {_loc} for all players
		
	victim is not the damager
	particleExtra(enchanted_hit, 20, 0, 0, 0, victim, 1)
	set {_v1} to vector from yaw victim's yaw and pitch 0
	set {_v2} to vector from yaw damager's yaw and pitch 0
	if victim's display name contains "'s Sentry" or "'s Dispenser":
		({_v1} dot {_v2}) is more than 0
		tfdmg(40, victim, damager)
	if ({_v1} dot {_v2}) is more than 0: 
	# TF2's Source code uses dot product, and honestly it works better than just doing abs(yaw - yaw)
		victim's display name does not contain "'s Sentry" or "'s Dispenser"
		set {_healby} to victim's health if damager's tool's name is "&6Conniver's Kunai"
		tfdmg(victim's max health * 60, victim, damager, 1)
		set custom model data of damager's tool to {_model}
		
		if damager's tool's name is "&6Conniver's Kunai":
			heal damager
			damager.setAbsorptionAmount(damager.getAbsorptionAmount() + {_healby})
			if damager.getAbsorptionAmount() > 23:
				damager.setAbsorptionAmount(23)
			
		
	else:
		tfdmg(40, victim, damager)
		